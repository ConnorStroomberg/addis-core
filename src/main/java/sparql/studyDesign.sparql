PREFIX ontology: <http://trials.drugis.org/ontology#>
PREFIX dataset: <http://trials.drugis.org/datasets/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX study: <http://trials.drugis.org/studies/>
PREFIX list: <http://jena.hpl.hp.com/ARQ/list#>
PREFIX instance: <http://trials.drugis.org/instances/>

SELECT 
  ?treatmentActivity
  ?epochLabel
  ?armLabel
  ?treatmentActivityType
  ?treatmentDrugLabel
  ?minValue
  ?minUnitLabel
  ?minDosingPeriodicity
  ?maxValue
  ?maxUnitLabel
  ?maxDosingPeriodicity
  ?fixedValue 
  ?fixedUnitLabel
  ?fixedDosingPeriodicity
WHERE {
  GRAPH dataset:$namespaceUid {
    ?dataset ontology:contains_study ?study .
  }
  GRAPH study:$studyUid {
    ?study rdfs:label ?label .
    ?study ontology:has_epochs ?epochs .
    ?epochs  list:member ?epoch .
    ?epoch rdfs:label ?epochLabel .
    OPTIONAL {
      ?participantFlow ontology:in_epoch ?epoch .
      ?participantFlow ontology:of_arm ?arm .
      ?arm rdfs:label ?armLabel .
    }
    ?treatmentActivity ontology:activity_application [
      ontology:applied_to_arm ?arm ;
      ontology:applied_in_epoch ?epoch
    ] .
    ?treatmentActivity a ?treatmentActivityType .
    OPTIONAL {
      ?treatmentActivity ontology:administered_drugs ?administeredDrugs .
      ?administeredDrugs list:member ?treatment .
      ?treatment ontology:treatment_has_drug ?drug .
      ?drug rdfs:label ?treatmentDrugLabel .
      OPTIONAL {
        ?treatment ontology:treatment_min_dose [ 
          rdf:value ?minValue ;
          ontology:dosingPeriodicity ?minDosingPeriodicity;
          ontology:units ?minUnit
        ] .
        ?minUnit rdfs:label ?minUnitLabel .
        ?treatment ontology:treatment_max_dose [ 
          ontology:units ?maxUnit ;
          rdf:value ?maxValue ;
          ontology:dosingPeriodicity ?maxDosingPeriodicity
        ] .
        ?maxUnit rdfs:label ?maxUnitLabel .
      }
      OPTIONAL {
        ?treatment ontology:treatment_dose [
          rdf:value ?fixedValue ;
          ontology:units ?fixedUnit ;
          ontology:dosingPeriodicity ?fixedDosingPeriodicity
        ] .
        ?fixedUnit rdfs:label ?fixedUnitLabel .
      }
    }
  }
}