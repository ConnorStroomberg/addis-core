PREFIX ontology: <http://trials.drugis.org/ontology#>
PREFIX dataset: <http://trials.drugis.org/datasets/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX list: <http://jena.hpl.hp.com/ARQ/list#>

PREFIX study: <http://trials.drugis.org/studies/>

SELECT ?study ?title ?label ?outcomeUids ?armUid ?drugUid  WHERE {
  GRAPH dataset:$namespaceUid {
   ?dataset ontology:contains_study ?study .
  }
  GRAPH ?study {
    ?study rdfs:label ?label .
    ?study rdfs:comment ?title .
    ?activity 
      a ontology:TreatmentActivity ;
      ontology:activity_application [
        ontology:applied_to_arm ?armUid
      ] ;
      ontology:administered_drugs ?administeredDrugs .
    ?administeredDrugs list:member ?administeredDrug .
    ?armUid rdfs:label ?armLabel .
    ?administeredDrug ontology:treatment_has_drug ?treatmentDrug .
    ?treatmentDrug rdfs:label ?drugLabel .
    ?treatmentDrug a ?drugUid .
    {
      SELECT ?study (group_concat(?outcomeUid; separator = ", ") as ?outcomeUids)
      WHERE {
        GRAPH ?study {
          ?study ontology:has_outcome ?outcomeInstanceUid .
          ?outcomeInstanceUid a ?outcomeUid .

        }
      } GROUP BY ?study
    }
  }
}
