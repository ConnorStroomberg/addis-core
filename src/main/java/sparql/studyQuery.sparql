PREFIX ontology: <http://trials.drugis.org/ontology#>
PREFIX dataset: <http://trials.drugis.org/datasets/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX list: <http://jena.hpl.hp.com/ARQ/list#>

PREFIX study: <http://trials.drugis.org/studies/>

SELECT ?study ?title ?label ?outcomeUids ?interventionUids WHERE {
  GRAPH dataset:$namespaceUid {
   ?dataset ontology:contains_study ?study .
  }
  GRAPH ?study {
    ?study rdfs:label ?label .
    ?study rdfs:comment ?title .
    { 
      # count the number of drug use in a treatment
      SELECT DISTINCT ?study (COUNT(?administeredDrug) as ?drugCount) 
      WHERE {
        ?study rdf:type ontology:Study . 
        ?activity 
          a ontology:TreatmentActivity ;
          ontology:administered_drugs ?administeredDrugs .
        ?administeredDrugs list:member ?administeredDrug .
      } GROUP BY ?study ?activity
    }
    {
      SELECT ?study (group_concat(?outcomeUid; separator = ", ") as ?outcomeUids)
      WHERE {
        GRAPH ?study {
          ?study ontology:has_outcome ?outcomeInstanceUid .
          ?outcomeInstanceUid a ?outcomeUid .

        }
      } GROUP BY ?study
    }
    {
      SELECT ?study (group_concat(?drugUid; separator = ", ") as ?interventionUids)
      WHERE {
        GRAPH ?dataset {
          ?drugUid rdfs:subClassOf ontology:Drug .
        }
        GRAPH ?study {
          ?instance a ?drugUid .
        }
      } GROUP BY ?study
    }
  }
  # filter out studies using combination treatments
  FILTER (?drugCount < 2)
}
