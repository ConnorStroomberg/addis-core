PREFIX ontology: <http://trials.drugis.org/ontology#>
PREFIX dataset: <http://trials.drugis.org/datasets/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX study: <http://trials.drugis.org/studies/>
PREFIX list: <http://jena.hpl.hp.com/ARQ/list#>
PREFIX instance: <http://trials.drugis.org/instances/>

SELECT
  ?studyDataTypeUri
  ?studyDataTypeLabel
  ?armInstanceUid
  ?armLabel
  ?sampleSize
  ?count
  ?mean
  ?std
  ?relativeToAnchor
  ?timeOffset
  ?duration
  ?relativeToEpochLabel
  {
  GRAPH dataset:$namespaceUid {
    ?dataset ontology:contains_study ?study .
    ?studyDataTypeUri rdfs:subClassOf ontology:$studyDataType ;
      rdfs:label ?studyDataTypeLabel .
   }
   GRAPH study:$studyUid {
     ?studyDataTypeInstance a ?studyDataTypeUri .
     ?study ontology:has_outcome ?studyDataTypeInstance .
     ?measurementJoinInstance ontology:of_outcome ?studyDataTypeInstance ;
         ontology:of_arm ?armInstanceUid ;
         ontology:of_moment ?momentInstance .
     ?measurementJoinInstance ontology:of_outcome ?studyDataTypeInstance ;
         ontology:of_arm ?armInstanceUid ;
         ontology:of_moment ?momentInstance .
     ?armInstanceUid rdfs:label ?armLabel ;
             a ontology:Arm .
     OPTIONAL {
          ?measurementJoinInstance ontology:sample_size ?sampleSize .
     }
     OPTIONAL {
         ?measurementJoinInstance ontology:count ?count .
     }
     OPTIONAL {
         ?measurementJoinInstance ontology:mean ?mean ;
         ontology:standard_deviation ?std .
     }
     ?momentInstance
         ontology:relative_to_epoch ?relativeToEpoch ;
         ontology:relative_to_anchor ?relativeToAnchor ;
         ontology:time_offset ?timeOffset .
     ?relativeToEpoch
         ontology:duration ?duration ;
         rdfs:label ?relativeToEpochLabel ;
         rdf:type ontology:Epoch .
   }
 }