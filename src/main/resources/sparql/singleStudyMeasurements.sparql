PREFIX ontology: <http://trials.drugis.org/ontology#>
PREFIX dataset: <http://trials.drugis.org/datasets/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX list: <http://jena.hpl.hp.com/ARQ/list#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX entity: <http://trials.drugis.org/entities/>
PREFIX concept: <http://trials.drugis.org/concepts/>
PREFIX instance: <http://trials.drugis.org/instances/>
PREFIX graph: <http://trials.drugis.org/graphs/>
PREFIX qudt: <http://qudt.org/schema/qudt#>



SELECT DISTINCT * WHERE {
  GRAPH graph:$studyUid {
    ?study rdfs:label ?studyName . # debug info
    ?study ontology:has_outcome ?outcomeInstance .
    $outcomeUnionString .
    ?outcomeInstance
      rdfs:label ?outcomeInstanceLabel ;
      ontology:of_variable [
        owl:sameAs ?outcomeTypeUri
      ] .
    $interventionUnionString .
    ?interventionInstance
      owl:sameAs ?interventionTypeUri ;
      rdfs:label ?interventionLabel .
    ?study a ontology:Study .
    ?study ontology:has_arm ?arm .
    ?study ontology:has_primary_epoch ?epoch .
    ?activity a ontology:TreatmentActivity ;
      ontology:has_activity_application [
        ontology:applied_to_arm ?arm ;
        ontology:applied_in_epoch ?epoch
      ] ;
      ontology:has_drug_treatment ?treatmentNode .
   ?treatmentNode ontology:treatment_has_drug ?drugInstance .

#    #?epoch rdfs:label ?epochLabel .  # debug info
#    #?arm rdfs:label ?armLabel .  # debug info
    OPTIONAL {
        ?treatmentNode
          a ontology:FixedDoseDrugTreatment ;
          a ?treatmentTypeOptional ;
          ontology:treatment_dose [
            rdf:value ?fixedDoseValue ;
            ontology:dosingPeriodicity ?fixedDoseDosingPeriodicity ;
            ontology:unit ?fixedDoseUnit
          ] .
          ?fixedDoseUnit
            a ?fixedDoseUnitConcept ;
            rdfs:label ?fixedDoseUnitLabel ;
            qudt:conversionMultiplier ?fixedDoseUnitMultiplier .
    } .
    OPTIONAL {
        ?treatmentNode
          a ontology:TitratedDoseDrugTreatment ;
          a ?treatmentTypeOptional ;
          ontology:treatment_max_dose [
            rdf:value ?maxDoseValue ;
            ontology:dosingPeriodicity ?maxDoseDosingPeriodicity ;
            ontology:unit ?maxDoseUnit
          ] ;
          ontology:treatment_min_dose [
                      rdf:value ?minDoseValue ;
                      ontology:dosingPeriodicity ?minDoseDosingPeriodicity ;
                      ontology:unit ?minDoseUnit
           ] .
          ?maxDoseUnit
            a ?maxDoseUnitConcept ;
            rdfs:label ?maxDoseUnitLabel ;
            qudt:conversionMultiplier ?maxDoseUnitMultiplier .
          ?minDoseUnit
            a ?minDoseUnitConcept ;
            rdfs:label ?minDoseUnitLabel ;
            qudt:conversionMultiplier ?minDoseUnitMultiplier .
    } .
#    # also get the measurement while we're here

    ?measurementMoment
      ontology:relative_to_epoch ?epoch ;
      ontology:relative_to_anchor ontology:anchorEpochEnd ;
      ontology:time_offset "PT0S"^^xsd:duration .

    ?measurement
      ontology:of_moment ?measurementMoment ;
      ontology:of_outcome ?outcomeInstance ;
      ontology:of_group ?arm .
    OPTIONAL {
      ?measurement
        ontology:mean ?mean ;
        ontology:standard_deviation ?stdDev ;
        ontology:sample_size ?sampleSize .
    }
    OPTIONAL {
      ?measurement
        ontology:count ?count ;
        ontology:sample_size ?sampleSize .
    }
  }
}