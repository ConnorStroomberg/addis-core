PREFIX ontology: <http://trials.drugis.org/ontology#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX graph: <http://trials.drugis.org/graphs/>

PREFIX entity: <http://trials.drugis.org/entities/>
PREFIX instance: <http://trials.drugis.org/instances/>
PREFIX concept: <http://trials.drugis.org/concepts/>
PREFIX ontology: <http://trials.drugis.org/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?graph ?value ?populationCharacteristicCovariateKey WHERE {
  GRAPH ?graph {

    BIND ('$populationCharacteristicUuid' AS ?populationCharacteristicCovariateKey) .

    OPTIONAL {
      ?study
        ontology:has_included_population ?overallGroup ;
        ontology:has_outcome ?popcharInstance ;
        ontology:has_primary_epoch ?epoch .

      ?popcharInstance
        a ontology:PopulationCharacteristic ;
        ontology:of_variable [
          owl:sameAs concept:$populationCharacteristicUuid
        ] .

      ?zeroMoment
        ontology:relative_to_epoch ?epoch ;
        ontology:relative_to_anchor ontology:anchorEpochStart ;
        ontology:time_offset "PT0S"^^xsd:duration .

      {
        ?measurementInstance
          ontology:of_outcome ?popcharInstance ;
          ontology:of_group ?overallGroup ;
          ontology:of_moment ?zeroMoment ;
          ontology:mean ?value .
      }
      UNION
      {
        ?measurementInstance
          ontology:of_outcome ?popcharInstance ;
          ontology:of_group ?overallGroup ;
          ontology:of_moment ?zeroMoment ;
          ontology:count ?count ;
          ontology:sample_size ?sampleSize .
        BIND (?count / ?sampleSize AS ?value)
      }
    }
  }
}