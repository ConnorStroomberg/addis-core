PREFIX ontology: <http://trials.drugis.org/ontology#>

DELETE {
  GRAPH <$graphUri> {
    ?s ontology:has_activity <$activityUri> . 
    <$activityUri> ?activityProperty ?activityObject . 
    ?treatmentUri ?treatmentProperty ?treatmenObject . 
    ?dose ?doseProperty ?doseValue .
    <$activityUri> ontology:has_activity_application ?application .
    ?application 
      ontology:applied_to_arm ?armUri ;
      ontology:applied_in_epoch ?epochUri .
  }
}
WHERE {
  GRAPH <$graphUri> {
    ?s ontology:has_activity <$activityUri> . 
    <$activityUri> ?activityProperty ?activityObject .
    OPTIONAL {
    	<$activityUri> ontology:has_drug_treatment ?treatmentUri . 
    	?treatmentUri ?treatmentProperty ?treatmenObject .
    	?treatmentUri ontology:treatment_min_dose|ontology:treatment_max_dose|ontology:treatment_dose ?dose .
    	?dose ?doseProperty ?doseValue .
    }
    OPTIONAL {
      <$activityUri> ontology:has_activity_application ?application .
      ?application 
        ontology:applied_to_arm ?armUri ;
        ontology:applied_in_epoch ?epochUri .
    }
  }
}
