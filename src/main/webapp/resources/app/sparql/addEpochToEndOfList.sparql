PREFIX instance: <http://trials.drugis.org/instances/>
PREFIX ontology: <http://trials.drugis.org/ontology#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# List of length >= 1
WITH <$graphUri>
DELETE { ?element rdf:rest rdf:nil }
INSERT { ?element rdf:rest [ rdf:first instance:$elementToInsert ; rdf:rest rdf:nil ] }
WHERE
{
  ?study ontology:has_epochs ?list .
  ?list rdf:rest* ?element .
  ?element rdf:rest rdf:nil .
} ;

# List of length = 0
WITH <$graphUri>
DELETE { ?study ontology:has_epochs rdf:nil . }
INSERT { ?study ontology:has_epochs [ rdf:first instance:$elementToInsert ; rdf:rest rdf:nil ] }
WHERE
{
   ?study ontology:has_epochs rdf:nil .
}


