PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX ontology: <http://trials.drugis.org/ontology#>
PREFIX dataset: <http://trials.drugis.org/datasets/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT
  ?graphUri
  ?studyUri
  ?title
  ?label
  ?allocation
  ?blinding
  ?objective
  ?inclusionCriteria
  (COUNT(distinct ?arm) as ?numberOfArms)
  (SUM(distinct ?numberOfParticipantsStarting) as ?studySize)
  (group_concat(DISTINCT ?publication; separator = ', ') as ?publications)
  (group_concat(DISTINCT ?drugName; separator = ', ') as ?drugNames)
  (group_concat(DISTINCT ?drug; separator = ', ') as ?drugUris)
  (group_concat(DISTINCT ?outcomeConcept; separator = ', ') as ?outcomeUris)
  (group_concat(DISTINCT ?activity; separator = ', ') as ?activities)
  ?status
  ?numberOfCenters
  ?indication
  ?startDate
  ?endDate
  ?doseType
WHERE {
  GRAPH ?graphUri {
      ?studyUri
        rdfs:label ?label ;
        a ontology:Study ;
        rdfs:comment ?title .
      OPTIONAL {
        ?studyUri ontology:has_outcome ?outcomeInstance .
        ?outcomeInstance
            ontology:of_variable [
                owl:sameAs ?outcomeConcept
            ]
      }
      OPTIONAL {
        ?studyUri ontology:has_allocation ?allocation .
      }
      OPTIONAL {
        ?studyUri ontology:has_blinding ?blinding .
      }
      OPTIONAL {
        ?studyUri ontology:has_objective [
          rdfs:comment ?objective
        ] .
      }
      OPTIONAL {
        ?studyUri ontology:has_eligibility_criteria [
          rdfs:comment ?inclusionCriteria
        ] .
      }
      OPTIONAL {
        ?studyUri ontology:status ?status .
      }
      OPTIONAL {
        ?studyUri ontology:has_number_of_centers ?numberOfCenters .
      }
      OPTIONAL {
        ?studyUri ontology:has_indication ?indication_instance .
        ?indication_instance rdfs:label ?indication .
      }
      OPTIONAL {
        ?studyUri ontology:has_start_date ?startDate .
      }
      OPTIONAL {
        ?studyUri ontology:has_end_date ?endDate .
      }
      OPTIONAL {
       SELECT ?studyUri ?doseType
         WHERE {
          BIND ('Flexible' as ?doseType)
           ?activity a ontology:TreatmentActivity ;
             ontology:has_activity_application [
               ontology:applied_to_arm ?arm
             ] ;
             ontology:has_drug_treatment [ a ontology:TitratedDoseDrugTreatment ] .
             ?studyUri ontology:has_arm ?arm .
          } GROUP BY ?studyUri ?doseType
            HAVING (COUNT(*) > 0)
      }
      OPTIONAL
      {
        ?instance owl:sameAs ?drug .
        ?instance a ontology:Drug .
        ?instance rdfs:label ?drugName .
      }
      OPTIONAL
      {
        ?studyUri ontology:has_publication [
          ontology:has_id ?publication
        ] .
      }
      OPTIONAL {
        ?arm a ontology:Arm .
        OPTIONAL {
          ?participantsStarting ontology:of_group ?arm .
          ?participantsStarting ontology:participants_starting ?numberOfParticipantsStarting .
        }
      }
    }
} GROUP BY ?graphUri ?studyUri ?title ?label ?allocation ?blinding ?objective ?inclusionCriteria ?status ?numberOfCenters ?indication ?startDate ?endDate ?doseType

