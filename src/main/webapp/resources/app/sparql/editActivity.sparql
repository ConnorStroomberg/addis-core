PREFIX instance: <http://trials.drugis.org/instances/>
PREFIX ontology: <http://trials.drugis.org/ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX study: <http://trials.drugis.org/studies/>

WITH <$graphUri>
DELETE {
  <$activityUri>
    rdfs:label ?oldLabel ;
    rdf:type ?oldActivityType .
  <$activityUri> rdfs:comment ?oldComment .
}
INSERT {
  <$activityUri>
    rdfs:label "$label" ;
    rdf:type <$activityTypeUri> .
}
WHERE {
  study:$studyUuid ontology:has_activity <$activityUri> .
  <$activityUri>
    rdfs:label ?oldLabel ;
    rdf:type ?oldActivityType .
    OPTIONAL {
      <$activityUri> rdfs:comment ?oldComment .
    } 
};

# clean-up any hanging treatment-stuff in case of switch from 
# treatment-type to non-treatment-type acticity
DELETE {
  GRAPH <$graphUri> { 
    ?treatmentUri ?treatmentProperty ?treatmenObject . 
    ?dose ?doseProperty ?doseValue .
  }
}
WHERE {
  GRAPH <$graphUri> {
    ?activityUri ontology:has_drug_treatment ?treatmentUri . 
    ?treatmentUri ?treatmentProperty ?treatmenObject .
    ?treatmentUri ontology:treatment_min_dose|ontology:treatment_max_dose|ontology:treatment_dose ?dose .
    ?dose ?doseProperty ?doseValue .
    FILTER NOT EXISTS { ?activityUri a ontology:TreatmentActivity . }
  }
};