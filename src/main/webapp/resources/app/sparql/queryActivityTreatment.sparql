PREFIX ontology: <http://trials.drugis.org/ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX study: <http://trials.drugis.org/studies/>

SELECT * WHERE {
  GRAPH <$graphUri> {
    study:$studyUuid ontology:has_activity ?activityUri .
    ?activityUri
      rdf:type ontology:TreatmentActivity ;
      ontology:has_drug_treatment ?treatmentUri .
      ?treatmentUri
        rdf:type ?treatmentDoseType ;
        ontology:treatment_has_drug ?treatmentDrugUri .
      ?treatmentDrugUri rdfs:label ?treatmentDrugLabel .
      ?treatmentUnitUri rdfs:label ?treatmentUnitLabel .
      {
        ?treatmentUri 
          rdf:type ontology:TitratedDoseDrugTreatment ;
          ontology:treatment_min_dose [
            rdf:value ?treatmentMinValue ;
            ontology:units ?treatmentUnitUri ;
            ontology:dosingPeriodicity ?treatmentDosingPeriodicity
          ] ;
          ontology:treatment_max_dose [
            rdf:value ?treatmentMaxValue ; ;
            ontology:units ?treatmentUnitUri ;
            ontology:dosingPeriodicity ?treatmentDosingPeriodicity
          ].
      }
      UNION 
      {
        ?treatmentUri 
          rdf:type ontology:FixedDoseDrugTreatment ;
          ontology:treatment_dose [
          rdf:value ?treatmentFixedValue ;
          ontology:units ?treatmentUnitUri ;
          ontology:dosingPeriodicity ?treatmentDosingPeriodicity
        ]
      }
  }
}