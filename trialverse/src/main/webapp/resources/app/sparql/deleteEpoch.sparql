PREFIX ontology: <http://trials.drugis.org/ontology#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX instance: <http://trials.drugis.org/instances/>
PREFIX study:  <http://trials.drugis.org/studies/>

# remove epoch triples
WITH <$graphUri>
DELETE {
  <$URI> ?p ?o .
}
WHERE {
  <$URI> ?p ?o .
};

# remove primary if set
WITH <$graphUri>
DELETE {
	?study ontology:has_primary_epoch <$URI> .
}
WHERE {
  ?study ontology:has_primary_epoch <$URI> .
};

# remove from list
  # if first item
WITH <$graphUri>
DELETE {
	?study ontology:has_epochs ?list .
	?list rdf:first <$URI> .
	?list rdf:rest ?tail .
}
INSERT {
	?study ontology:has_epochs ?tail
}
WHERE {
	?study ontology:has_epochs ?list .
	?list rdf:first <$URI> .
	?list rdf:rest ?tail .
};

  # if not first item
WITH <$graphUri>
DELETE {
  ?prevNode rdf:rest ?element .
  ?element rdf:first <$URI> .
  ?element rdf:rest ?tail .
}
INSERT {
	?prevNode rdf:rest ?tail .
}
WHERE {
  ?study ontology:has_epochs ?list .
  ?list rdf:rest* ?element .
  ?prevNode rdf:rest ?element .
  ?element rdf:first <$URI> .
  ?element rdf:rest ?tail .
};



