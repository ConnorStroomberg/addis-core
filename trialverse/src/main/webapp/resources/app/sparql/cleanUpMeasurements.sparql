PREFIX instance: <http://trials.drugis.org/instances/>
PREFIX ontology: <http://trials.drugis.org/ontology#>

# Delete measurements where outcome, measurement moment, or arm doesn't exist.
DELETE {
  GRAPH <$graphUri> {
	  ?measurement ?p ?o
  }
} WHERE { 
  GRAPH <$graphUri> {
    {
      ?measurement ontology:of_outcome ?s .
  	} UNION {
      ?measurement ontology:of_moment ?s .
  	} UNION {
      ?measurement ontology:of_arm ?s .
   	}
    FILTER NOT EXISTS { ?s ?x ?y }
    ?measurement ?p ?o
  }
};

# Delete measurements where the outcome is not measured at the specified moment.
DELETE {
  GRAPH <$graphUri> {
    ?measurement ?p ?o
  }
} WHERE { 
  GRAPH <$graphUri> {
    ?measurement 
      ontology:of_outcome ?outcome ;
      ontology:of_moment ?moment .
    FILTER NOT EXISTS { ?outcome ontology:is_measured_at ?moment }
    ?measurement ?p ?o .
  }
};

# Delete results properties that aren't specified for the outcome.
# TODO: generalize the "VALUES" statement to something based on subPropertyOf.
DELETE {
  GRAPH <$graphUri> {
    ?measurement ?result_property ?result_value
  }
} WHERE {
  GRAPH <$graphUri> {
    VALUES ?result_property { ontology:mean ontology:standard_deviation ontology:count ontology:sample_size }
    ?measurement ontology:of_outcome ?outcome .
    FILTER NOT EXISTS { ?outcome ontology:has_result_property ?result_property }
    ?measurement ?result_property ?result_value .
  }
}