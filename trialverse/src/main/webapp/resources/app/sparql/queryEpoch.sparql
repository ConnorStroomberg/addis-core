prefix ontology: <http://trials.drugis.org/ontology#>
prefix study: <http://trials.drugis.org/studies/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX list: <http://jena.hpl.hp.com/ARQ/list#>

select ?uri ?label ?comment ?duration ?isPrimary ?pos {
  graph <$graphUri> {
   ?uri rdfs:label ?label .
   ?study ontology:has_epochs ?list .
   ?list list:index (?pos ?uri) .
   OPTIONAL { ?uri rdfs:comment ?comment .}
   OPTIONAL { ?uri ontology:duration ?duration .}
   BIND ( EXISTS {
    ?study ontology:has_primary_epoch ?uri .
    } AS ?isPrimary )
 }
}

